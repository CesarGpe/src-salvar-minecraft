import mc2:technical/event/main as module
import mc2:technical/setup/main as module

import mc2:madness/main as module
import mc2:items/spider_cowl as module
import mc2:items/navigoggles/main as module
import mc2:items/recovery_compass/main as module

# Storage should all be contained in the same compound for easy resets.
# "mc2:storage data" will be this compound, so we can delete storage with:
# data remove storage mc2:storage data

function mc2:load:
	# THIS FUNCTION IS MODIFIED IN setup.py AND finish.py !!!!!
	raw # Runs on reload, or just before loading the world
	gamerule doLimitedCrafting true
	gamerule doInsomnia false

	# Store world spawn
	execute summon minecraft:marker run function mc2:technical/setup/get_world_spawn
	# Ensure consistent advancement tab order
	function mc2:technical/setup/order_adv

function mc2:tick:
	raw # Runs every game tick
	raw # 1 irl second = 20 game ticks
	execute as @a at @s run function mc2:player_tick

function mc2:player_tick:
	execute if entity @s[tag=mc2.wearing_spider_cowl] unless predicate mc2:wearing_spider_cowl:
		effect clear @s minecraft:night_vision
		tag @s remove mc2.wearing_spider_cowl

function mc2:technical/second_clock:
	#item replace entity @e[type=minecraft:drowned,nbt={equipment:{mainhand:{id:"minecraft:trident"}}}] weapon.mainhand with iron_sword 1
	execute as @e[type=minecraft:drowned,tag=!mc2.drowned.checked,tag=!smithed.entity,tag=!smithed.strict]:
		execute if items entity @s weapon.mainhand minecraft:trident run item replace entity @s weapon.mainhand with minecraft:iron_sword
		tag @s add mc2.drowned.checked

	execute as @e[type=minecraft:armadillo,tag=!smithed.entity,tag=!smithed.strict] at @s:
		execute if entity @s[tag=!mc2.armadillo.scared] if entity @a[distance=..6]:
			data modify entity @s Brain.memories."minecraft:danger_detected_recently" set value {value:true}
			tag @s add mc2.armadillo.scared
		execute if entity @s[tag=mc2.armadillo.scared]:
			execute if entity @a[distance=..6] run return run data modify entity @s Brain.memories."minecraft:danger_detected_recently" set value {value:true}
			data modify entity @s Brain.memories."minecraft:danger_detected_recently" set value {value:false,ttl:60}
			tag @s remove mc2.armadillo.scared
		execute if data entity @s {state:scared} run return run data modify entity @s Invulnerable set value true
		data modify entity @s Invulnerable set value false

function mc2:technical/ten_second_clock:
	execute as @a at @s run function mc2:madness/calculate

append function_tag minecraft:load {
    "values": [
        "mc2:load",
    ]
}

append function_tag minecraft:tick {
    "values": [
        "mc2:tick",
    ]
}
